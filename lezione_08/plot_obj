# gnuplot scritp

# setup dell'output
set terminal pdf size 8, 5 font "Helvetica, 12"
set output "plot_obj.pdf"

set xrange [ -5 : 10 ]          # range dell'asse X
set yrange [ -5 : 10 ]          # range dell'asse Y
set zrange [ -5 : 10 ]          # range dell'asse Z
set xlabel "X"                  # nome dell'asse X
set ylabel "Y"                  # nome dell'asse Y
set zlabel "Z"                  # nome dell'asse Z
set xyplane at 0                # imposta il piano XY allo 0 della Z
set view equal xyz              # imposta la stessa scala per i tre assi

set pm3d implicit                       # riempie le superfici
unset colorbox                          # rimuove la barra laterale dei colori

set pm3d lighting primary 0.4           # imposta una sorgente di luce per creare ombre
set style fill transparent solid 0.65   # imposta la trasparenza delle superfici

# crea una palette di tre colori
set cbrange [ 1 : 3 ]
set palette defined ( 1 "#ff4c4d", 2 "#4c4cef", 3 "#ceff9e" )

# plotta tre parallelepipedi da tre file diversi
splot   "obj1.txt" using 1:2:3:(1) title "Before" with linespoints pointtype 7, \
        "obj2.txt" using 1:2:3:(2) title "After Basic" with linespoints pointtype 7, \
        "obj3.txt" using 1:2:3:(3) title "After Eigen" with linespoints pointtype 7