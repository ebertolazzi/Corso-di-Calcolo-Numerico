# gnuplot scritp

# setup dell'output
set terminal gif animate delay 20 size 800, 600
set output "plot.gif"

# segna l'asse X alla posizione y=0
set xzeroaxis

# estrae una statistica preventiva dai dataset di un file,
# qui serve solo per sapere quanti 'dataset' ci sono
# (ovvero quanti sono i passi eseguiti dall'algoritmo ricorsivo)
stats "plot_anim.txt" nooutput

# il primo 'dataset' corrisponde alla funzione
# tutti i successivi sono i punti che fanno da intervallo per il metodo adattativo
# questi vengono plottati in un loop
do for [ i = 1:int( STATS_blocks ) ] {
	plot	"plot_anim.txt" index 0 title "f(x)" with lines linetype 6 linewidth 2, \
			"plot_anim.txt" index 1:i title "intervals" with impulses linetype 7 noautoscale
}